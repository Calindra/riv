## pragmas.unitname = ''

require 'memory'
require '.riv_types'
require '.riv_xoshiro256'

global function riv_init_input(): void
  -- track only gamepad keys by default
  riv.mmio_driver.tracked_keys[RIV_GAMEPAD_RIGHT] = true
  riv.mmio_driver.tracked_keys[RIV_GAMEPAD_LEFT] = true
  riv.mmio_driver.tracked_keys[RIV_GAMEPAD_DOWN] = true
  riv.mmio_driver.tracked_keys[RIV_GAMEPAD_UP] = true
  riv.mmio_driver.tracked_keys[RIV_GAMEPAD_A1] = true
  riv.mmio_driver.tracked_keys[RIV_GAMEPAD_A2] = true
  riv.mmio_driver.tracked_keys[RIV_GAMEPAD_A3] = true
  riv.mmio_driver.tracked_keys[RIV_GAMEPAD_A4] = true
  riv.mmio_driver.tracked_keys[RIV_GAMEPAD_L1] = true
  riv.mmio_driver.tracked_keys[RIV_GAMEPAD_L2] = true
  riv.mmio_driver.tracked_keys[RIV_GAMEPAD_L3] = true
  riv.mmio_driver.tracked_keys[RIV_GAMEPAD_R1] = true
  riv.mmio_driver.tracked_keys[RIV_GAMEPAD_R2] = true
  riv.mmio_driver.tracked_keys[RIV_GAMEPAD_R3] = true
  riv.mmio_driver.tracked_keys[RIV_GAMEPAD_START] = true
  riv.mmio_driver.tracked_keys[RIV_GAMEPAD_SELECT] = true
end

local function riv_append_keystroke_entropy(key_code: uint8, down: boolean, frame: uint64): void
  local h: uint64 = 0
  h = h | key_code
  h = h | (down and (1_u64 << 8) or 0_u64)
  h = h | (frame << 9)
  riv.entropy[riv.entropy_index] = h
  riv.entropy_index = (riv.entropy_index + 1) & (#riv.entropy - 1)
  if riv.entropy_size < #riv.entropy then
    riv.entropy_size = riv.entropy_size + 1
  end
end

global function riv_poll_inputs(): void
  for i: uint32=0,<#riv.keys do
    riv.keys[i].press = false
    riv.keys[i].release = false
  end
  local key_toggle_count: uint32 = riv.mmio_device.key_toggle_count
  if key_toggle_count > 0 then
    for i: uint32=0,<key_toggle_count do
      local key_code: uint8 = riv.mmio_device.key_toggles[i]
      local down: boolean = not riv.keys[key_code].down
      local frame: uint64 = riv.frame
      riv.key_toggles[i] = key_code
      riv.keys[key_code].down = down
      riv.keys[key_code].up = not down
      if down then
        riv.keys[key_code].press = true
        riv.keys[key_code].down_frame = frame
      else -- up
        riv.keys[key_code].release = true
        riv.keys[key_code].up_frame = frame
      end
      riv_append_keystroke_entropy(key_code, down, frame)
    end
    local entropy: span(uint8) = {data=(@*[0]uint8)(&riv.entropy),size=riv.entropy_size * #@uint64}
    riv.prng:srand_entropy(entropy)
  end
  for i: uint32=key_toggle_count,<riv.key_toggle_count do
    riv.key_toggles[i] = 0
  end
  riv.key_toggle_count = key_toggle_count
end
