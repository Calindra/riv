require 'memory'
require 'string'
require 'linux'

require '.riv_types'
require '.riv_prng'

local span_uint8: type = @span(uint8)

local riv_yield_request: type = @record{
  dev: uint8,
  mode: uint8,
  reason: uint16,
  data: uint32,
}

function riv_context:_init_default_palette(): void
  self.mmio_driver.palette[RIV_PAL16_BLACK] = RIV_RGB_PAL16_BLACK
  self.mmio_driver.palette[RIV_PAL16_DARKBLUE] = RIV_RGB_PAL16_DARKBLUE
  self.mmio_driver.palette[RIV_PAL16_DARKPURPLE] = RIV_RGB_PAL16_DARKPURPLE
  self.mmio_driver.palette[RIV_PAL16_DARKGREEN] = RIV_RGB_PAL16_DARKGREEN
  self.mmio_driver.palette[RIV_PAL16_BROWN] = RIV_RGB_PAL16_BROWN
  self.mmio_driver.palette[RIV_PAL16_DARKGREY] = RIV_RGB_PAL16_DARKGREY
  self.mmio_driver.palette[RIV_PAL16_LIGHTGREY] = RIV_RGB_PAL16_LIGHTGREY
  self.mmio_driver.palette[RIV_PAL16_WHITE] = RIV_RGB_PAL16_WHITE
  self.mmio_driver.palette[RIV_PAL16_RED] = RIV_RGB_PAL16_RED
  self.mmio_driver.palette[RIV_PAL16_ORANGE] = RIV_RGB_PAL16_ORANGE
  self.mmio_driver.palette[RIV_PAL16_YELLOW] = RIV_RGB_PAL16_YELLOW
  self.mmio_driver.palette[RIV_PAL16_GREEN] = RIV_RGB_PAL16_GREEN
  self.mmio_driver.palette[RIV_PAL16_BLUE] = RIV_RGB_PAL16_BLUE
  self.mmio_driver.palette[RIV_PAL16_LAVENDER] = RIV_RGB_PAL16_LAVENDER
  self.mmio_driver.palette[RIV_PAL16_PINK] = RIV_RGB_PAL16_PINK
  self.mmio_driver.palette[RIV_PAL16_LIGHTPEACH] = RIV_RGB_PAL16_LIGHTPEACH
end

function riv_context:_yield(reason: riv_control_command): void
  if self.yield_fd <= 0 then return end
  local IOCTL_YIELD: usize <comptime> = 0xc008d100
  local HTIF_DEVICE_YIELD: uint8 <comptime> = 2
  local HTIF_YIELD_AUTOMATIC: uint8 <comptime> = 0
  local swap_yield_req: riv_yield_request = {
    dev = HTIF_DEVICE_YIELD,
    mode = HTIF_YIELD_AUTOMATIC,
    reason = reason,
    data = 0,
  }
  assert(ioctl(self.yield_fd, IOCTL_YIELD, (@usize)(&swap_yield_req)) == 0, 'yield ioctl failed')
end

function riv_context:_init_prng(argc: int32, argv: *[0]cstring): void
  local hash: [2]uint64 = {self.incard_format, self.incard_format}
  if self.incard_len > 0 then
    hash = murmur3_128(span_uint8{data=(self.inoutbuffer), size=self.incard_len}, hash)
  end
  if argc > 0 and argv ~= nilptr then
    for i: int32=1,<argc do
      if argv[i] ~= nilptr then
        hash = murmur3_128(span_uint8{data=(@*[0]byte)(argv[i]), size=#argv[i]}, hash)
      end
    end
  end
  self.prng:srand(hash[0], hash[1])
end

function riv_context:_read_replay_next_key_event(): void
  if self.replay_next_key_event.key_code == 0xff then return end
  local ret: isize = read(self.replay_fd, &self.replay_next_key_event, #@riv_key_toggle_event)
  assert(ret == #@riv_key_toggle_event, 'read replay fd failed')
end

function riv_context:setup(argc: int32, argv: *[0]cstring): void <cexport'riv_setup'>
  assert(self, 'invalid riv context')
  memory.zero(self, #@riv_context)
  -- yield
  local no_yield_str: string = getenv('RIV_NO_YIELD')
  if #no_yield_str == 0 then
    self.yield_fd = open("/dev/yield", O_RDWR, 0)
    assert(self.yield_fd > 0, 'open yield device failed')
    self.yielding = true
  end
  -- map memory
  assert(get_free_hugepages() == 1, 'no free hugepage')
  local prot: cint = PROT_READ | PROT_WRITE
  local flags: cint = MAP_ANONYMOUS | MAP_SHARED | MAP_FIXED_NOREPLACE | MAP_POPULATE | MAP_HUGETLB
  local mmio: pointer = (@pointer)(RIV_VADDR_BASE)
  assert(mmap(mmio, RIV_MMIOSIZE_HUGEPAGE, prot, flags, -1, 0) == mmio, 'mmap failed')
  assert(get_free_hugepages() == 0, 'no used hugepage')
  -- initialize device
  memory.zero(mmio, RIV_MMIOSIZE_HUGEPAGE)
  self.mmio_driver = (@*riv_mmio_driver)(RIV_VADDR_MMIO_DRIVER)
  self.mmio_device = (@*riv_mmio_device)(RIV_VADDR_MMIO_DEVICE)
  self.mmio_driver.header = {
    magic = RIV_DRIVER_MAGIC,
    version = RIV_VERSION,
    features = 0,
    uuid = 0
  }
  self.mmio_driver.framebuffer_desc = {
    width = RIV_DEFAULT_WIDTH,
    height = RIV_DEFAULT_HEIGHT,
    target_fps = RIV_DEFAULT_TARGET_FPS,
    pixel_format = RIV_DEFAULT_PIXELFORMAT,
  }
  self.inoutbuffer = (@*[RIV_MMIOSIZE_INOUTBUFFER]byte)(RIV_VADDR_INOUTBUFFER)
  self.framebuffer = (@*[RIV_MMIOSIZE_FRAMEBUFFER]byte)(RIV_VADDR_FRAMEBUFFER)
  self.audiobuffer = (@*[RIV_MMIOSIZE_AUDIOBUFFER]byte)(RIV_VADDR_AUDIOBUFFER)
  -- aliases
  self.framebuffer_desc = &self.mmio_driver.framebuffer_desc
  self.tracked_keys = &self.mmio_driver.tracked_keys
  self.palette = &self.mmio_driver.palette
  -- track all keys by default
  memory.spanset((@span(boolean))(self.mmio_driver.tracked_keys), true)
  -- init palette
  self:_init_default_palette()
  -- retrieve input card from host
  self:_yield(RIV_CONTROL_OPEN)
  self.incard_len = self.mmio_device.incard_len & (RIV_MMIOSIZE_INOUTBUFFER-1)
  self.incard_format = self.mmio_device.incard_format
  -- initialize prng
  self:_init_prng(argc, argv)
  self.valid = true

  local replay_fd_str: string = getenv('RIV_REPLAY_FD')
  if #replay_fd_str > 0 then
    self.replaying = true
    self.replay_fd = (@int32)(tointeger(replay_fd_str))
    self:_read_replay_next_key_event()
  end
end

function riv_context:shutdown(): void <cexport'riv_shutdown'>
  -- ignore when not initialized
  if not self or not self.valid then return end
  -- copy outcard description to driver
  self.mmio_driver.outcard_format = self.outcard_format
  self.mmio_driver.outcard_len = self.outcard_len
  -- tell the host we finished, the host may use inoutbuffer
  self:_yield(RIV_CONTROL_CLOSE)
  -- unmap memory
  local mmio: pointer = (@pointer)(RIV_VADDR_BASE)
  memory.zero(mmio, RIV_MMIOSIZE_HUGEPAGE)
  assert(munmap(mmio, RIV_MMIOSIZE_HUGEPAGE) == 0, 'munmap failed')
  assert(get_free_hugepages() == 1, 'no freed hugepage')
  -- close yield device
  if self.yield_fd > 0 then
    assert(close(self.yield_fd) >= 0, 'close yield fd failed')
  end
  memory.zero(self, #@riv_context)
end

function riv_context:_append_keystroke_entropy(key_code: uint8, down: boolean, frame: uint64): void
  local h: uint64 = 0
  h = h | key_code
  h = h | (down and (1_u64 << 8) or 0_u64)
  h = h | (frame << 9)
  self.entropy[self.entropy_index] = h
  self.entropy_index = (self.entropy_index + 1) & (#self.entropy - 1)
  if self.entropy_size < #self.entropy then
    self.entropy_size = self.entropy_size + 1
  end
end

function riv_context:_poll_inputs(): void
  for i: uint32=0,<#self.keys do
    self.keys[i].down = false
    self.keys[i].up = false
  end
  if self.mmio_device.key_toggle_count > 0 then
    for i: uint32=0,<self.mmio_device.key_toggle_count do
      local key_code: uint8 = self.mmio_device.key_toggles[i]
      local press: boolean = not self.keys[key_code].press
      local frame: uint64 = self.frame
      self.keys[key_code].press = press
      if press then
        self.keys[key_code].down = true
        self.keys[key_code].down_frame = frame
      else
        self.keys[key_code].up = true
        self.keys[key_code].up_frame = frame
      end
      self:_append_keystroke_entropy(key_code, press, frame)
    end
    local entropy: span(byte) = {data=(@*[0]uint8)(&self.entropy),size=self.entropy_size * #@uint64}
    self.prng:srand_entropy(entropy)
  end
end

function riv_context:_flush_audio(): void
  self:_yield(RIV_CONTROL_FLUSH_AUDIO)
  self.mmio_driver.audio_command_len = 0
  self.audiobuffer_off = 0
end

function riv_context:present(): void <cexport'riv_present'>
  assert(self and self.valid, 'invalid riv context')
  -- force exit when replay ends
  if self.replaying and self.replay_next_key_event.key_code == 0xff and self.frame >= self.replay_next_key_event.frame + 1 then
    self:shutdown()
    exit(0)
    return
  end
  -- copy outcard description to driver
  self.mmio_driver.outcard_format = self.outcard_format
  self.mmio_driver.outcard_len = self.outcard_len
  -- present
  self:_yield(RIV_CONTROL_PRESENT)
  -- fill with replay inputs
  if self.replaying then
    local key_index: uint32 = 0
    while self.replay_next_key_event.frame == self.frame and self.replay_next_key_event.key_code ~= 0xff do
      self.mmio_device.key_toggles[key_index] = self.replay_next_key_event.key_code
      key_index = key_index + 1
      self:_read_replay_next_key_event()
    end
    self.mmio_device.key_toggle_count = key_index
  end
  -- advance frame
  self.frame = self.frame + 1
  self.mmio_driver.frame = self.frame
  -- audio commands was flushed
  self.mmio_driver.audio_command_len = 0
  self.audiobuffer_off = 0
  -- poll keyboard inputs
  self:_poll_inputs()
end

function riv_context:loop(frame_cb: function(riv: *riv_context)): void <cexport'riv_loop'>
  assert(self and self.valid, 'invalid riv context')
  while not self.quit do
    frame_cb(self)
    self:present()
  end
end

function riv_context:_queue_audio_command(command: riv_audio_command): void
  local command_idx: uint32 = self.mmio_driver.audio_command_len
  self.mmio_driver.audio_commands[command_idx] = command
  self.mmio_driver.audio_command_len = command_idx + 1
  if self.mmio_driver.audio_command_len >= #self.mmio_driver.audio_commands then
    self:_flush_audio()
  end
end

function riv_context:make_sound_buffer(desc: *riv_sound_buffer_desc): uint64 <cexport'riv_make_sound_buffer'>
  assert(self and self.valid, 'invalid riv context')
  -- silently ignore invalid id field
  if desc.id ~= 0 then return 0 end
  -- TODO: we could send in multiple chunks
  assert(desc.data.size <= RIV_MMIOSIZE_AUDIOBUFFER, 'sound buffer is too large')
  -- flush previous audio buffer if there is not enough space
  if self.audiobuffer_off + desc.data.size > RIV_MMIOSIZE_AUDIOBUFFER then
    self:_flush_audio()
  end
  -- copy buffer
  memory.copy(&self.audiobuffer[self.audiobuffer_off], desc.data.data, desc.data.size)
  desc.data.data = (@*byte)((@usize)(self.audiobuffer_off))
  self.audiobuffer_off = self.audiobuffer_off + desc.data.size
  -- choose sound buffer id
  desc.id = self.sound_buffer_gen + 1
  self.sound_buffer_gen = desc.id
  -- queue command
  self:_queue_audio_command{
    type = RIV_AUDIOCOMMAND_MAKE_SOUND_BUFFER,
    desc = {sound_buffer = $desc},
  }
  return desc.id
end

function riv_context:destroy_sound_buffer(id: uint64): void <cexport'riv_destroy_sound_buffer'>
  assert(self and self.valid, 'invalid riv context')
  -- queue command
  self:_queue_audio_command{
    type = RIV_AUDIOCOMMAND_DESTROY_SOUND_BUFFER,
    desc = {sound_buffer = {id = id}},
  }
end

function riv_context:sound(desc: *riv_sound_desc): uint64 <cexport'riv_sound'>
  assert(self and self.valid, 'invalid riv context')
  -- choose sound id
  if desc.id == 0 then
    if desc.buffer_id == 0 then return 0 end -- nothing to play
    desc.id = self.sound_gen + 1
    self.sound_gen = desc.id
  end
  -- queue command
  self:_queue_audio_command{
    type = RIV_AUDIOCOMMAND_SOUND,
    desc = {sound = $desc}
  }
  return desc.id
end

function riv_context:waveform(desc: *riv_waveform_desc): uint64 <cexport'riv_waveform'>
  assert(self and self.valid, 'invalid riv context')
  -- choose sound id
  desc.id = self.sound_gen + 1
  self.sound_gen = desc.id
  -- queue command
  self:_queue_audio_command{
    type = RIV_AUDIOCOMMAND_WAVEFORM,
    desc = {waveform = $desc}
  }
  return desc.id
end

global function riv_run(desc: *riv_run_desc): void <cexport'riv_run'>
  local self: riv_context
  if desc.framebuffer_desc.width == 0 then desc.framebuffer_desc.width = RIV_DEFAULT_WIDTH end
  if desc.framebuffer_desc.height == 0 then desc.framebuffer_desc.height = RIV_DEFAULT_HEIGHT end
  if desc.framebuffer_desc.target_fps == 0 then desc.framebuffer_desc.target_fps = RIV_DEFAULT_TARGET_FPS end
  if desc.framebuffer_desc.pixel_format == 0 then desc.framebuffer_desc.pixel_format = RIV_DEFAULT_PIXELFORMAT end
  if not self.mmio_driver then
    self:setup(desc.argc, desc.argv)
  end
  if desc.init_cb then
    desc.init_cb(self)
  end
  self.mmio_driver.framebuffer_desc = desc.framebuffer_desc
  if desc.frame_cb then
    self:loop(desc.frame_cb)
  end
  if desc.cleanup_cb then
    desc.cleanup_cb(self)
  end
  self:shutdown()
  exit(0)
end
